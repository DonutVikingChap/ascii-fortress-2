import file menus/af2_menu
import file lib/gui

// Running this file toggles the menu.
if menu_is_active("font") {
	// Save the settings.
	host_writeconfig
	
	// Remove the menu.
	menu_pop "font"
}
else {
	menu_begin "font"

	// Element ids.
	const id_screen menu_new_id()
	const id_font_settings_text menu_new_id()

	const id_back_button menu_new_id()

	const id_auto_font_size_text menu_new_id()
	const id_auto_font_size_checkbox menu_new_id()

	const id_font_scale_text menu_new_id()
	const id_font_scale_slider menu_new_id()
	const id_font_scale_input menu_new_id()

	const id_font_size_text menu_new_id()
	const id_font_size_slider menu_new_id()
	const id_font_size_input menu_new_id()

	const id_ratio_text menu_new_id()
	const id_ratio_slider menu_new_id()
	const id_ratio_input menu_new_id()

	const id_font_text menu_new_id()
	const id_font_dropdown menu_new_id()
	
	const id_reset_button menu_new_id()

	array fonts {
		"Liberation"
		"Unscii"
		"Spleen"
		"Cozette"
		"Galmuri"
	}

	array font_filenames {
		"liberation/LiberationMono-Regular.ttf"
		"unscii/unscii-8.ttf"
		"spleen/spleen-8x16.otf"
		"cozette/CozetteVector.ttf"
		"galmuri/Galmuri11-Bold.ttf"
	}

	function find_font_index {
		const i find_index(font_filenames $r_font)
		if lt($i size(fonts)) {
			return $i
		}
		return 0
	}

	function on_font_dropdown_changed {
		const index gui_get_value($id_font_dropdown)
		r_font font_filenames($index)
	}

	const scale_min 0.5
	const scale_max 2.5
	const scale_delta 0.05

	const size_min 8
	const size_max 48
	const size_delta 0.025

	const ratio_min 0.6
	const ratio_max 1.6
	const ratio_delta 0.1

	function reset_to_defaults {
		reset r_font_match_size
		reset r_font_match_size_coefficient
		reset r_font_size
		reset r_ratio
		reset r_font
		gui_set_value $id_auto_font_size_checkbox $r_font_match_size
		gui_set_value $id_font_scale_slider div(sub($r_font_match_size_coefficient $scale_min) sub($scale_max $scale_min))
		gui_set_text $id_font_scale_input $r_font_match_size_coefficient
		gui_set_value $id_font_size_slider div(sub($r_font_size $size_min) sub($size_max $size_min))
		gui_set_text $id_font_size_input $r_font_size
		gui_set_value $id_ratio_slider div(sub($r_ratio $ratio_min) sub($ratio_max $ratio_min))
		gui_set_text $id_ratio_input $r_ratio
		gui_set_value $id_font_dropdown find_font_index()
	}

	// Element layout.
	// command                  id                           x  y   w  h  color  text                   function                                                          association
	gui_screen_file             $id_screen                   0  0         white  "menu_background.txt"  
	gui_text                    $id_font_settings_text       3  3         white  "Font settings"
	
	gui_button                  $id_back_button              3  6   11 5  white  "<- Back"              {font_settings}
	
	gui_text                    $id_auto_font_size_text      33 3         white  "Use auto scale"
	gui_cvar_checkbox           $id_auto_font_size_checkbox  29 3   3  1  white                         r_font_match_size
	
	gui_text                    $id_font_scale_text          29 6         white  "Auto scale"
	gui_cvar_slider_with_input  $id_font_scale_slider        29 7   13 1  white                         r_font_match_size_coefficient $scale_min $scale_max $scale_delta  $id_font_scale_input
	gui_cvar_input_with_slider  $id_font_scale_input         43 6   9  3  white                         r_font_match_size_coefficient $scale_min $scale_max               $id_font_scale_slider
	
	gui_text                    $id_font_size_text           29 10        white  "Manual size"
	gui_cvar_slider_with_input  $id_font_size_slider         29 11  13 1  white                         r_font_size $size_min $size_max $size_delta                       $id_font_size_input
	gui_cvar_input_with_slider  $id_font_size_input          43 10  9  3  white                         r_font_size $size_min $size_max                                   $id_font_size_slider
	
	gui_text                    $id_ratio_text               29 14        white  "Ratio"
	gui_cvar_slider_with_input  $id_ratio_slider             29 15  13 1  white                         r_ratio $ratio_min $ratio_max $ratio_delta                        $id_ratio_input
	gui_cvar_input_with_slider  $id_ratio_input              43 14  9  3  white                         r_ratio $ratio_min $ratio_max                                     $id_ratio_slider
	
	gui_text                    $id_font_text                29 18        white  "Font"
	gui_dropdown                $id_font_dropdown            29 19  17 1  white  find_font_index()      {on_font_dropdown_changed}                                        fonts...

	gui_button                  $id_reset_button             3  22  21 5  white  "Reset to defaults"    {reset_to_defaults}
	
	// Add the menu.
	gui_push_menu --on_select_none {open_console} --on_escape {font_settings} \
		$id_back_button \
		$id_auto_font_size_checkbox \
		$id_font_scale_slider \
		$id_font_scale_input \
		$id_font_size_slider \
		$id_font_size_input \
		$id_ratio_slider \
		$id_ratio_input \
		$id_font_dropdown \
		$id_reset_button
}
