import file menus/af2_menu
import file lib/gui

// Running this file toggles the menu.
if menu_is_active("multiplayer") {
	// Save the settings.
	host_writeconfig
	
	// Remove the menu.
	menu_pop "multiplayer"
}
else {
	menu_begin "multiplayer"

	// Element ids.
	const id_screen menu_new_id()
	const id_multiplayer_settings_text menu_new_id()

	const id_back_button menu_new_id()
    
	const id_cl_showping_text menu_new_id()
	const id_cl_showping_checkbox menu_new_id()

	const id_cl_mouselook_text menu_new_id()
	const id_cl_mouselook_checkbox menu_new_id()

	const id_crosshair_text menu_new_id()

	const id_cl_crosshair_enable_text menu_new_id()
	const id_cl_crosshair_enable_checkbox menu_new_id()

	const id_cl_crosshair_use_team_color_text menu_new_id()
	const id_cl_crosshair_use_team_color_checkbox menu_new_id()

	const id_cl_crosshair_collide_world_text menu_new_id()
	const id_cl_crosshair_collide_world_checkbox menu_new_id()

	const id_cl_crosshair_distance_follow_cursor_text menu_new_id()
	const id_cl_crosshair_distance_follow_cursor_checkbox menu_new_id()

	const id_cl_crosshair_min_distance_text menu_new_id()
	const id_cl_crosshair_min_distance_slider menu_new_id()
	const id_cl_crosshair_min_distance_input menu_new_id()

	const id_cl_crosshair_max_distance_text menu_new_id()
	const id_cl_crosshair_max_distance_slider menu_new_id()
	const id_cl_crosshair_max_distance_input menu_new_id()

	const id_cl_crosshair_text menu_new_id()
	const id_cl_crosshair_input menu_new_id()

	const id_cl_crosshair_color_text menu_new_id()
	const id_cl_crosshair_color_input menu_new_id()

	const id_crosshair_preview_text menu_new_id()
	const id_crosshair_preview_screen menu_new_id()
	const id_crosshair_preview_player menu_new_id()
	const id_crosshair_preview_string menu_new_id()

	const id_reset_button menu_new_id()

	function on_crosshair_changed {
		gui_remove $id_crosshair_preview_string
		if $cl_crosshair_enable {
			var crosshair_distance 20
			if and(le($cl_crosshair_min_distance $cl_crosshair_max_distance) lt($cl_crosshair_max_distance 20)) {
				crosshair_distance $cl_crosshair_max_distance
			}
			if $cl_crosshair_collide_world {
				if gt($crosshair_distance 9) {
					crosshair_distance 9
				}
			}
			var crosshair_color $cl_crosshair_color
			if $cl_crosshair_use_team_color {
				crosshair_color team_red
			}
			gui_text $id_crosshair_preview_string add(5 $crosshair_distance) 19 $crosshair_color $cl_crosshair
		}
		else {
			gui_text $id_crosshair_preview_string 5 19 white ""
		}
	}

	const distance_min 0
	const distance_max 25
	const distance_delta 0.04

	function reset_to_defaults {
		reset cl_showping
		reset cl_mouselook
		reset cl_crosshair_enable
		reset cl_crosshair_use_team_color
		reset cl_crosshair_collide_world
		reset cl_crosshair_distance_follow_cursor
		reset cl_crosshair_min_distance
		reset cl_crosshair_max_distance
		reset cl_crosshair
		reset cl_crosshair_color
		gui_set_value $id_cl_showping_checkbox $cl_showping
		gui_set_value $id_cl_mouselook_checkbox $cl_mouselook
		gui_set_value $id_cl_crosshair_enable_checkbox $cl_crosshair_enable
		gui_set_value $id_cl_crosshair_use_team_color_checkbox $cl_crosshair_use_team_color
		gui_set_value $id_cl_crosshair_collide_world_checkbox $cl_crosshair_collide_world
		gui_set_value $id_cl_crosshair_distance_follow_cursor_checkbox $cl_crosshair_distance_follow_cursor
		gui_set_value $id_cl_crosshair_min_distance_slider div(sub($cl_crosshair_min_distance $distance_min) sub($distance_max $distance_min))
		gui_set_text $id_cl_crosshair_min_distance_input $cl_crosshair_min_distance
		gui_set_value $id_cl_crosshair_max_distance_slider div(sub($cl_crosshair_max_distance $distance_min) sub($distance_max $distance_min))
		gui_set_text $id_cl_crosshair_max_distance_input $cl_crosshair_max_distance
		gui_set_text $id_cl_crosshair_input $cl_crosshair
		gui_set_text $id_cl_crosshair_color_input $cl_crosshair_color
		on_crosshair_changed
	}

	// Element layout.
	// command                           id                                                x  y   w  h  color  text                    function                                                               association
	gui_screen_file                      $id_screen                                        0  0         white  "menu_background.txt"
	gui_text                             $id_multiplayer_settings_text                     3  3         white  "Multiplayer settings"

	gui_button                           $id_back_button                                   3  6   11 5  white  "<- Back"               {multiplayer_settings}
	
	gui_text                             $id_cl_showping_text                              7  12        white  "Show ping"
	gui_cvar_checkbox                    $id_cl_showping_checkbox                          3  12  3  1  white                          cl_showping

	gui_text                             $id_cl_mouselook_text                             7  14        white  "Enable mouselook"
	gui_cvar_checkbox                    $id_cl_mouselook_checkbox                         3  14  3  1  white                          cl_mouselook

	gui_text                             $id_crosshair_text                                29 3         white  "Crosshair:"

	gui_text                             $id_cl_crosshair_enable_text                      33 5         white  "Enable"
	gui_cvar_checkbox_callback           $id_cl_crosshair_enable_checkbox                  29 5   3  1  white                          cl_crosshair_enable                                                    {on_crosshair_changed}

	gui_text                             $id_cl_crosshair_use_team_color_text              33 7         white  "Use team color"
	gui_cvar_checkbox_callback           $id_cl_crosshair_use_team_color_checkbox          29 7   3  1  white                          cl_crosshair_use_team_color                                            {on_crosshair_changed}

	gui_text                             $id_cl_crosshair_collide_world_text               33 9         white  "Collide with world"
	gui_cvar_checkbox_callback           $id_cl_crosshair_collide_world_checkbox           29 9   3  1  white                          cl_crosshair_collide_world                                             {on_crosshair_changed}

	gui_text                             $id_cl_crosshair_distance_follow_cursor_text      33 11        white  "Follow cursor"
	gui_cvar_checkbox                    $id_cl_crosshair_distance_follow_cursor_checkbox  29 11  3  1  white                          cl_crosshair_distance_follow_cursor
	
	gui_text                             $id_cl_crosshair_min_distance_text                29 13        white  "Min. distance"
	gui_cvar_slider_with_input_callback  $id_cl_crosshair_min_distance_slider              29 14  13 1  white                          cl_crosshair_min_distance $distance_min $distance_max $distance_delta  $id_cl_crosshair_min_distance_input   {on_crosshair_changed}
	gui_cvar_input_with_slider_callback  $id_cl_crosshair_min_distance_input               43 13  9  3  white                          cl_crosshair_min_distance $distance_min $distance_max                  $id_cl_crosshair_min_distance_slider  {on_crosshair_changed}

	gui_text                             $id_cl_crosshair_max_distance_text                29 17        white  "Max. distance"
	gui_cvar_slider_with_input_callback  $id_cl_crosshair_max_distance_slider              29 18  13 1  white                          cl_crosshair_max_distance $distance_min $distance_max $distance_delta  $id_cl_crosshair_max_distance_input   {on_crosshair_changed}
	gui_cvar_input_with_slider_callback  $id_cl_crosshair_max_distance_input               43 17  9  3  white                          cl_crosshair_max_distance $distance_min $distance_max                  $id_cl_crosshair_max_distance_slider  {on_crosshair_changed}

	gui_text                             $id_cl_crosshair_text                             29 21        white  "Character\n_________________"
	gui_cvar_input_callback              $id_cl_crosshair_input                            47 21  5  3  white                          cl_crosshair                                                           {on_crosshair_changed}

	gui_text                             $id_cl_crosshair_color_text                       29 25        white  "Color\n_______"
	gui_cvar_input_callback              $id_cl_crosshair_color_input                      37 25  15 3  white                          cl_crosshair_color                                                     {on_crosshair_changed}

	gui_text                             $id_crosshair_preview_text                        3  16        white  "Crosshair preview:"
	gui_screen_matrix                    $id_crosshair_preview_screen                      3  17        white  concat( \
		"+----------------------+\n" \
		"|           #          |\n" \
		"|           #          |\n" \
		"|           #          |\n" \
		"+----------------------+")
	gui_text                             $id_crosshair_preview_player                      5  19        team_red  "@*"
	gui_text                             $id_crosshair_preview_string                      5  19        white  ""
	
	gui_button                           $id_reset_button                                  3  23  21 5  white  "Reset to defaults"     {reset_to_defaults}
	
	on_crosshair_changed
	
	// Add the menu.
	gui_push_menu --on_select_none {open_console} --on_escape {multiplayer_settings} \
		$id_back_button \
		$id_cl_showping_checkbox \
		$id_cl_mouselook_checkbox \
		$id_cl_crosshair_enable_checkbox \
		$id_cl_crosshair_use_team_color_checkbox \
		$id_cl_crosshair_collide_world_checkbox \
		$id_cl_crosshair_distance_follow_cursor_checkbox \
		$id_cl_crosshair_min_distance_slider \
		$id_cl_crosshair_min_distance_input \
		$id_cl_crosshair_max_distance_slider \
		$id_cl_crosshair_max_distance_input \
		$id_cl_crosshair_input \
		$id_cl_crosshair_color_input \
		$id_reset_button
}
