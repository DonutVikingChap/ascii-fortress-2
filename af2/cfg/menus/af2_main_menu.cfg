import file menus/af2_menu

// Try to disconnect in case this file is run when in-game.
try {
	disconnect
	// Note: After disconnecting, this file will automatically run again
	// (assuming that "main" is set to import this file).
}
catch {
	// If disconnecting failed, that means we are not in-game. Proceed as normal.
	menu_begin "main"
	
	// Element ids.
	const id_screen menu_new_id()
	const id_game_name_text menu_new_id()
	const id_join_menu_button menu_new_id()
	const id_server_menu_button menu_new_id()
	const id_settings_button menu_new_id()
	const id_quit_button menu_new_id()
	
	// Calculate the middle of the screen.
	var middle floor(div(gui_size_x() 2))
	
	// Calculate X coordinate for the game name text.
	var x1 sub($middle floor(div(strlen($game_name) 2)))
	
	// Calculate X coordinate for the buttons.
	var x2 sub($middle 8)
	
	// Element layout.
	// command       id                      x   y   w  h  color  text                   function
	gui_screen_file  $id_screen              0   0         white  "menu_background.txt"
	gui_text         $id_game_name_text      $x1 3         white  $game_name
	
	gui_button       $id_join_menu_button    $x2 6   16 5  white  "Join Game"            {join_menu}
	
	gui_button       $id_server_menu_button  $x2 13  16 5  white  "Start Server"         {server_menu}
	
	gui_button       $id_settings_button     $x2 20  16 5  white  "Settings"             {settings}
	
	gui_button       $id_quit_button         50  0   5  3  white  "X"                    {quit}
	
	// Add the menu.
	// Note: Pressing escape selects the quit button.
	gui_push_menu --on_select_none {open_console} --on_escape concat("gui_deactivate; gui_activate " $id_quit_button) \
		$id_join_menu_button \
		$id_server_menu_button \
		$id_settings_button \
		$id_quit_button
}
